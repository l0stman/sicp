\documentclass[a4paper,12pt]{article}
\usepackage{listings}
\lstset{language=Lisp}

\begin{document}
\begin{lstlisting}
(define (merge-weighted s1 s2 weight)
  (cond ((stream-null? s1) s2)
        ((stream-null? s2) s1)
        (else
         (let ((s1car (stream-car s1))
               (s2car (stream-car s2)))
           (cond ((< (weight s1car) (weight s2car))
                  (cons-stream
                   s1car
                   (merge-weighted (stream-cdr s1)
                                   s2
                                   weight)))
                 ((> (weight s1car) (weight s2car))
                  (cons-stream
                   s2car
                   (merge-weighted s1
                                   (stream-cdr s2)
                                   weight)))
                 (else
                  (cons-stream
                   s1car
                   (cons-stream
                    s2car
                    (merge-weighted (stream-cdr s1)
                                    (stream-cdr s2)
                                    weight)))))))))

(define (weighted-pairs s t weight)
  (cons-stream
   (list (stream-car s) (stream-car t))
   (merge-weighted
    (stream-map (lambda (x) (list (stream-car s) x))
                (stream-cdr t))
    (weighted-pairs (stream-cdr s)
                    (stream-cdr t)
                    weight)
    weight)))

(define integers1
  (weighted-pairs integers
                  integers
                  (lambda (pair)
                    (+ (car pair) (cadr pair)))))

(define integers2
  (let ((stream (stream-filter
                 (lambda (i)
                   (not (or (zero? (remainder i 2))
                            (zero? (remainder i 3))
                            (zero? (remainder i 5)))))
                 integers)))
    (weighted-pairs
     stream
     stream
     (lambda (pair)
       (+ (* 2 (car pair))
          (* 3 (cadr pair))
          (* 5 (car pair) (cadr pair)))))))
\end{lstlisting}
\end{document}
