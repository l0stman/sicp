\documentclass[a4paper,12pt]{article}
\usepackage{listings}
\lstset{language=Lisp}
\newcommand{\subpar}[1]{\medskip \noindent #1.}

\begin{document}

First let's modify the representation of an instruction such that we
also keep track of the labels that immediately precede an instruction.

\begin{lstlisting}
(define (make-instruction text)
  (list text '()))
(define (instruction-text inst)
  (car inst))
(define (instruction-execution-proc inst)
  (cadr inst))
(define (instruction-preceding-labels inst)
  (cddr inst))
(define (set-instruction-execution-proc! inst proc)
  (set-car! (cdr inst) proc))
(define (add-preceding-label! inst label)
  (set-cdr! (cdr inst) (cons label (cddr inst))))
\end{lstlisting}

Then we modify \lstinline!extract-labels! to add a label to the
instruction that comes immediately after it if any.

\begin{lstlisting}
(define (extract-labels text receive)
  (if (null? text)
      (receive '() '())
      (extract-labels
       (cdr text)
       (lambda (insts labels)
         (let ((next-inst (car text)))
           (cond ((not (symbol? next-inst))
                  (receive (cons (make-instruction next-inst)
                                 insts)
                           labels))
                 ((assoc next-inst labels)
                  (error "label indicates two different locations"
                         next-inst))
                 (else
                  (when insts
                    (add-instruction-preceding-label!
                     (car insts) next-inst))
                  (receive insts
                      (cons (make-label-entry next-inst
                                              insts)
                            labels)))))))))
\end{lstlisting}

And finally, we modify \lstinline!execute! in
\lstinline!make-new-machine! to print the labels preceding an
instruction.

\begin{lstlisting}
(define (make-new-machine)
  (let ((pc (make-register 'pc))
        (flag (make-register 'flag))
        (stack (make-stack))
        (the-instruction-sequence '())
        (inst-count 0)
        (trace-on? #f))
    (let ((the-ops
           (list (list 'initialize-stack
                       (lambda () (stack 'initialize)))))
          (register-table
           (list (list 'pc pc) (list 'flag flag))))
      (define (allocate-register name)
        (if (assoc name register-table)
            (error "Multiply defined register: " name)
            (let ((reg (make-register name)))
              (set! register-table
                    (cons (list name reg) register-table))
              reg)))
      (define (lookup-register name)
        (let ((val (assoc name register-table)))
          (if val
              (cadr val)
              (allocate-register name))))
      (define (execute)
        (let ((insts (get-contents pc)))
          (if (null? insts)
              'done
              (let ((inst (car insts)))
                (when trace-on?
                  (for-each (lambda (label)
                              (display label)
                              (newline))
                            (instruction-preceding-labels inst))
                  (display (instruction-text inst))
                  (newline))
                ((instruction-execution-proc inst))
                (set! inst-count (+ inst-count 1))
                (execute)))))
      (define (dispatch message)
        (cond ((eq? message 'start)
               (set-contents! pc the-instruction-sequence)
               (execute))
              ((eq? message 'install-instruction-sequence)
               (lambda (seq) (set! the-instruction-sequence seq)))
              ((eq? message 'allocate-register) allocate-register)
              ((eq? message 'get-register) lookup-register)
              ((eq? message 'install-operations)
               (lambda (ops) (set! the-ops (append the-ops ops))))
              ((eq? message 'stack) stack)
              ((eq? message 'operations) the-ops)
              ((eq? message 'instruction-count) inst-count)
              ((eq? message 'reset-count) (set! inst-count 0))
              ((eq? message 'trace-on) (set! trace-on? #t))
              ((eq? message 'trace-off) (set! trace-on? #f))
              (else (error "Unknown request -- MACHINE" message))))
      dispatch)))
\end{lstlisting}

\end{document}
