\documentclass[a4paper,12pt]{article}
\usepackage{listings}
\lstset{language=Lisp}

\begin{document}
a. If we assume that $+$ and $*$ always take two arguments and
that expressions are fully parenthesized:
\begin{lstlisting}
(define (deriv exp var)
  (cond ((number? exp) 0)
        ((variable? exp)
         (if (same-variable? exp var) 1 0))
        ((sum? exp)
         (make-sum (deriv (addend exp) var)
                   (deriv (augend exp) var)))
        ((product? exp)
         (make-sum
          (make-product (multiplier exp)
                        (deriv (multiplicand exp) var))
          (make-product (deriv (multiplier exp) var)
                        (multiplicand exp)))) 
        (else
         (error "unknown expression type -- DERIV"
                exp))))

(define (variable? x) (symbol? x))
(define (same-variable? v1 v2)
  (and (variable? v1) (variable? v2) (eq? v1 v2)))

(define (make-sum a1 a2)
  (cond ((=number? a1 0) a2)
        ((=number? a2 0) a1)
        ((and (number? a1) (number? a2)) (+ a1 a2))
        (else (list a1 '+ a2))))

(define (=number? exp num)
  (and (number? exp) (= exp num)))

(define (sum? x)
  (and (pair? x) (eq? (cadr x) '+)))
(define (addend s) (car s))
(define (augend s) (caddr s))

(define (make-product m1 m2)
  (cond ((or (=number? m1 0) (=number? m2 0)) 0)
        ((=number? m1 1) m2)
        ((=number? m2 1) m1)
        ((and (number? m1) (number? m2)) (* m1 m2))
        (else (list m1 '* m2))))

(define (product? x)
  (and (pair? x) (eq? (cadr x) '*)))
(define (multiplier p) (car p))
(define (multiplicand p) (caddr p))
\end{lstlisting}

\medskip
b. Here we drops unnecessary parentheses and assumes that
multiplication is done before addition.
\begin{lstlisting}
(define (deriv exp var)
  (cond ((number? exp) 0)
        ((variable? exp)
         (if (same-variable? exp var) 1 0))
        ((sum? exp)
         (make-sum (deriv (addend exp) var)
                   (deriv (augend exp) var)))
        ((product? exp)
         (make-sum
          (make-product (multiplier exp)
                        (deriv (multiplicand exp) var))
          (make-product (deriv (multiplier exp) var)
                        (multiplicand exp))))
        (else
         (error "unknown expression type -- DERIV"
                exp))))

(define (variable? x) (symbol? x))
(define (same-variable? v1 v2)
  (and (variable? v1) (variable? v2) (eq? v1 v2)))

(define (simplify sym-proc acc vars args)
  (let ((proc (eval sym-proc)))
    (cond ((null? args)
           (cons acc (reverse vars)))
          ((number? (car args))
           (simplify sym-proc
                     (proc (car args) acc)
                     vars
                     (cdr args)))
          ((and (eq? sym-proc '+)
                (pair? (car args)))
           (simplify sym-proc
                     acc
                     (append (car args)
                             (cons sym-proc vars))
                     (cdr args)))
          (else
           (simplify sym-proc
                     acc
                     (cons (car args)
                           (cons sym-proc vars))
                     (cdr args))))))

(define (make-sum a1 a2 . rest)
  (let ((lst (simplify '+ 0 '()
                       (cons a1 (cons a2 rest)))))
    (cond ((null? (cdr lst)) (car lst))
          ((zero? (car lst))
           (if (null? (cdddr lst))
               (caddr lst)
               (cddr lst)))
          (else lst))))

(define (=number? exp num)
  (and (number? exp) (= exp num)))

(define (sum? x)
  (and (pair? x) (memq '+ x)))

(define (first-op e sym)
  (define (iter res lst)
    (cond ((null? lst)
           (error "expression doesn't contain -- "
                  sym))
          ((eq? (car lst) sym)
           (if (null? (cdr res))
               (car res)
               (reverse res)))
          (else
           (iter (cons (car lst) res)
                 (cdr lst)))))
  (iter '() e))

(define (second-op e sym)
  (let ((l (memq sym e)))
    (cond ((null? l)
           (error "expression doesn't contain -- "
                  sym))
          ((null? (cddr l)) (cadr l))
          (else (cdr l)))))

(define (addend s) (first-op s '+))
(define (augend s) (second-op s '+))
(define (make-product m1 m2 . rest)
  (let ((lst (simplify '* 1 '()
                       (cons m1 (cons m2 rest)))))
    (cond ((null? (cdr lst)) (car lst))
          ((zero? (car lst)) 0)
          ((= (car lst) 1)
           (if (null? (cdddr lst))
               (caddr lst)
               (cddr lst)))
          (else lst))))

(define (product? x)
  (and (pair? x) (not (sum? x)) (memq '* x)))

(define (multiplier p) (first-op p '*))
(define (multiplicand p) (second-op p '*))
\end{lstlisting}
\end{document}
